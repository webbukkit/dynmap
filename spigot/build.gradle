description = 'dynmap'

repositories {
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url 'https://repo.codemc.org/repository/maven-releases/'
    }
}

dependencies {
    implementation group: 'org.bukkit', name: 'bukkit', version: '1.7.10-R0.1-SNAPSHOT'
    implementation 'com.nijikokun.bukkit:Permissions:3.1.6'
    implementation 'me.lucko.luckperms:luckperms-api:4.3'
    implementation 'net.luckperms:api:5.0'
    implementation('com.github.MilkBowl:VaultAPI:1.7') { transitive = false }
    compileOnly 'net.skinsrestorer:skinsrestorer-api:14.1.+'
    implementation project(":dynmap-api")
    implementation project(path: ":DynmapCore", configuration: "shadow")
    implementation group: 'ru.tehkode', name: 'PermissionsEx', version: '1.19.1'
    implementation group: 'de.bananaco', name: 'bPermissions', version: '2.9.1'
    implementation group: 'com.platymuus.bukkit.permissions', name: 'PermissionsBukkit', version: '1.6'
    implementation group: 'org.anjocaido', name: 'EssentialsGroupManager', version: '2.10.1'
    implementation group: 'org.bstats', name: 'bstats-bukkit', version: '2.2.1'
    implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    implementation project(':bukkit-helper')
    implementation(project(':bukkit-helper-113-2')) {
        transitive = false
    }
    implementation(project(':bukkit-helper-114-1')) {
        transitive = false
    }
    implementation(project(':bukkit-helper-115')) {
        transitive = false
    }
    implementation(project(':bukkit-helper-116')) {
        transitive = false
    }
    implementation(project(':bukkit-helper-116-2')) {
        transitive = false
    }
    implementation(project(':bukkit-helper-116-3')) {
        transitive = false
    }
    implementation(project(':bukkit-helper-116-4')) {
        transitive = false
    }
    implementation(project(':bukkit-helper-117')) {
        transitive = false
    }
    implementation(project(':bukkit-helper-118')) {
    	transitive = false
    }
}

processResources {
    // replace stuff in mcmod.info, nothing else
    filesMatching('plugin.yml') {
        // replace version and mcversion
        expand(
                buildnumber: project.parent.ext.globals.buildNumber,
                version: project.version
        )
    }
}

jar {
    classifier = 'unshaded'
}

shadowJar {
    dependencies {
        include(dependency('org.bstats::'))
        include(dependency(':dynmap-api'))
        include(dependency(":DynmapCore"))
        include(dependency(':bukkit-helper'))
        include(dependency(':bukkit-helper-113-2'))
        include(dependency(':bukkit-helper-114-1'))
        include(dependency(':bukkit-helper-115'))
        include(dependency(':bukkit-helper-116'))
        include(dependency(':bukkit-helper-116-2'))
        include(dependency(':bukkit-helper-116-3'))
        include(dependency(':bukkit-helper-116-4'))
        include(dependency(':bukkit-helper-117'))
        include(dependency(':bukkit-helper-118'))
    }
    relocate('org.bstats', 'org.dynmap.bstats')
    destinationDir = file '../target'
    archiveName = "Dynmap-${parent.version}-spigot.jar"
    classifier = ''
}
shadowJar.doLast {
    task ->
        ant.checksum file: task.archivePath
}

artifacts {
    archives shadowJar
}
